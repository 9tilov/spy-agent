apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

apply from: "$project.rootDir/tools/findbugs.gradle"
apply from: "$project.rootDir/tools/checkstyle.gradle"
apply from: "$project.rootDir/tools/pmd.gradle"

android {

    signingConfigs {
        release
    }

    Properties props = new Properties()
    def propFile = file('../keystore.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

            signingConfigs.release.storeFile = file(props['STORE_FILE'])
            signingConfigs.release.storePassword = props['STORE_PASSWORD']
            signingConfigs.release.keyAlias = props['KEY_ALIAS']
            signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        }
    }

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.moggot.spyagent"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'RestrictedApi'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.0.2'

    implementation 'com.vk:androidsdk:1.6.9'

    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    implementation 'com.jakewharton.timber:timber:4.6.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'

    implementation 'android.arch.persistence.room:runtime:1.0.0'
    implementation 'android.arch.persistence.room:rxjava2:1.0.0'
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation 'com.google.dagger:dagger-android:2.13'
    implementation 'com.google.dagger:dagger-android-support:2.13'
    implementation 'com.android.support:support-v4:27.0.2'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.13'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.github.bumptech.glide:glide:4.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.4.0'
}
